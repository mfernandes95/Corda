buildscript {
    ext {
//        corda_release_distribution = 'com.r3.corda'
        corda_release_distribution = 'net.corda'
        corda_release_version = '4.3'
        corda_gradle_plugins_version = '5.0.8'
        kotlin_version = '1.3.61'
    }

    repositories {
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'net.corda.plugins.cordapp'

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://software.r3.com/artifactory/corda-dependencies" }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        cordaCompile "$corda_release_distribution:corda-core:$corda_release_version"
    }
}

dependencies {
    cordaCompile "$corda_release_distribution:corda-rpc:$corda_release_version"
    cordaRuntime "$corda_release_distribution:corda:$corda_release_version"

    cordapp project(":contracts")
    cordapp project(":workflows")
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    nodeDefaults {
        cordapp project(":contracts")
        cordapp project(":workflows")
    }
    node {
        name "O=Notary,L=Campinas,C=BR"
        notary = [validating : false]
        p2pAddress "localhost:11001"
        rpcSettings {
            address("localhost:10001")
            adminAddress("localhost:10041")
        }
    }
    node {
        name "O=PartyA,L=Campinas,C=BR"
        p2pAddress "localhost:11002"
        rpcSettings {
            address("localhost:10002")
            adminAddress("localhost:10042")
        }
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
}